% top-level simulation entry point

\begin{vdm_al}
class World

values
	-- controller strategy identifier (sdp)
	public sid : real = 0.0
	
instance variables
	-- variable used to block the high-prio simulation user thread 
	static public done : bool := false;
	
operations
	-- entry point for DESTECS co-simulation
	public run: () ==> ()
	run () == (
		-- create the environment instance
		dcl env : Environment := 
			if sid <= 0.0
			then new AbstractDeEnvironment()
			else new Environment();
		
		-- diagnostics
		IO`printf("Simulation started (%s)\n", [sid]);
		
		-- initialize the environment
		env.init();
		
		-- start the torsion bar controller
		cases (sid) :
			3 		-> TorsionBar`ctrl.startUp(<ADVANCED>),
			2 		-> TorsionBar`ctrl.startUp(<SIMPLE>),
			others 	-> TorsionBar`ctrl.startUp(<ABSTRACT>)
		end;
		
		-- wait for the simulation to terminate
		block();
		
		-- diagnostics
		IO`print("Simulation stopped at ");
		IO`print(time / 1E9);
		IO`println(" seconds");
	);

	-- auxiliary operation used to 
	public block: () ==> ()
	block () == skip
	
sync
	-- the operation block is yielded until done is set to true
	per block => done

end World
\end{vdm_al}